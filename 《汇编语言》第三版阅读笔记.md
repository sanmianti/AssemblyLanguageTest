# **《汇编语言》第三版阅读笔记**
---
## **第一章 基础知识**
汇编课程研究重点放在如何利用硬件系统的编程结构和指令集有效灵活的控制系统进行工作。

### **1.1 机器语言**
机器语言是机器指令的集合。电子计算机的机器指令是一列二进制数字。计算机将之转换为一列高低电平，以使计算机的电子器件受到驱动，进行运算。

每一种微处理器都有自己的机器指令集，也就是机器语言。

### **1.2 汇编语言的产生**
机器语言难以辩别和记忆，基于此人们发明了汇编语言。

**寄存器** 简单的讲是CPU中（内部）可以存储数据的器件。  
**编译器** 能够将汇编指令转换为机器指令的翻译程序。

### **1.3 汇编语言的组成**
汇编语言主要由以下3类指令组成：  

（1） 汇编指令：机器码的助记符，有对应的机器码。  
（2） 伪指令：没有对应的机器码，由编译器执行，计算机并不执行。  
（3） 其他符号：如 +、-、*、/等，由编译器识别，没有对应的机器码。

### **1.4 存储器**

### **1.5 指令和数据**
指令和数据是应用上的概念。在内存或磁盘上，指令和数据没有任何区别，都是二进制信息。

### **1.6 存储单元**
计算机内的最小信息单位是bit，即一个二进制位。  
计算机内的基本存储单元是Byte，即一个字节。一个字节等于8个二进制位。  
1KB = 1024B, 1MB = 1024KB, 1GB = 1024MB, 1TB = 1024GB

### **1.7 CPU 对存储器的读写**
CPU通过地址总线给出数据存储位置。  
CPU通过控制总线给出数据存储方向。     
CUP通过数据总线进行数据传输。

### **1.8 地址总线**
一个CPU有N根地址线，则可以说这个CPU的地址总线宽度为N。这样的CPU最多可以寻找2的N次方个内存单元。

### **1.9 数据总线**
数据总线的宽度决定了CPU和外界的数据传送速度。

### **1.10 控制总线**
CPU对外部器件的控制是通过控制总线来进行的。控制总线是一些不同控制线的集合。

### **1.1~1.10 小结**

>（1） 汇编指令是机器指令的助记符，同机器指令一一对应。   
（2） 每一种CPU都有自己的汇编指令集。   
（3） CPU可以直接使用的信息在存储器中存放。  
（4） 在存储器中指令和数据没有任何区别，都是二进制信息。  
（5） 存储单元从零开始顺序编号。
（6） 一个存储单元可以存储8个bit，即8位二进制数。  
（7） 1Byte = 8bit， 1KB = 1024B = 2^10B， 1MB = 1024KB = 2^20B, 1GB = 1024MB = 2^30B。2^10 = 1024, 2^16 = 65536。
（8） 每一个CPU芯片都有许多管脚，这些管脚和总线相连。也可以说，这些管脚引出总线。一个CPU可以引出3中总线的宽度标志了这个CPU的不同方面的性能：  
  地址总线的宽度决定了CPU的寻址能力；  
  数据总线的宽度决定了CPU与其他器件进行数据传送时一次数据传送量；  
  控制总线的宽度决定了CPU对系统中其他器件的控制能力。

### **1.11 内存地址空间（概述）**

  内存地址空间就是CPU可以通过地址总线寻址到的内存单元集合。

### **1.12 主板**

### **1.13 接口卡**

  CPU通过总线向接口卡发送命令，接口卡根据CPU的命令控制外设工作。如：网卡、显卡、声卡等。

### **1.14 各类存储器芯片**

  **RAM:** 随机存储器，可读可写，但必须带电存储，断电后存储内容消失。  
  **ROM:** 只读存储器，只能读出，不能写入。断电后存储内容不消失。  
  **BIOS:** Basic Input/Output System，基本输入输出系统。BIOS是由主板和各类接口卡（如显卡、网卡等）厂商提供的软件系统。 可以通过它利用该硬件设备进行最基本的输入输出。在主板和某些接口卡上茶油存储相应BIOS的ROM。例如，主板上的ROM中存储着主板的BIOS(通常称为系统BIOS)；显卡上的ROM存储着显卡的BIOS；如果网卡上装有ROM，那其中就可以存储网卡的BIOS。

### **1.15 内存地址空间**
  最终运行程序的是CPU，我们用汇编语言编程的时候，必须要从CPU的角度考虑问题。对CPU来讲，系统中所有存储器中的存储单元都处于一个统一的逻辑存储器中，它的容量受CPU寻址能力的限制。这个逻辑存储器即是我们所说的内存地址空间。

  ---

## **第二章 寄存器**
  一个典型的CPU由运算器、控制器、寄存器等器件构成：   
  - 运算器进行信息处理；  
  - 寄存器进行信息存储；  
  - 控制器控制各种器件进行工作；  
  - 内部总线连接各种器件，在它们之间进行数据的传送。  

寄存器是CPU内部的存储器件。  

8086CPU内部有14个寄存器，分别是：AX、BX、CX、DX、SI、DI、SP、BP、IP、CS、SS、DS、ES、PSW。这14个寄存器都是16位的。

### **2.1 通用寄存器**

AX、BX、CX、DX这这四个寄存器通常用来存放一般性数据，被称为通用寄存器。  

为了保证向前兼容，8086CPU的AX、BX、CX、DX这4个寄存器可以分为两个独立使用的8位寄存器来用：  
  - AX可以分为AH和AL;
  - BX可以分为BH和BL;
  - CX可以分为CH和CL;
  - DX可以分为DH和DL。

### **2.2 字在寄存器中的存储**

对于8086CPU来说，一个字由两个字节组成，这两个字节分别称之为高位字节和低位字节，并且存储于寄存器中的高8位和低8位。

### **2.3 几条汇编指令**

mov ax, 001AH——转移指令  
add ax, bx——求和指令

### **2.4 物理地址**

所有内存单元构成的存储空间是一个一维线性空间，每一个内存单元在这个空间都有唯一的地址，我们将这个唯一的地址称为物理地址。

### **2.5 16位结构的CPU**

8086CPU是16位结构的CPU，这也就是说，在8086内部，能够一次性处理、传输、暂时存储的信息的最大长度是16位的。内存单元的地址在送上地址总线之前，必须在CPU中处理、传输、暂时存放，对于16位CPU，能一次性处理、传输、暂时存储16位的地址。

但8086CPU有20根地址总线，那么16位的8086CPU是如何给出20位的地址总线的呢？

### **2.6 8086CPU给出物理地址的方法**

8086CPU地址总线长度大于字长，导致程序物理地址无法一次性传递给CPU。为此，8086CPU采用一种在内部用两个16位地址合成的方法来形成一个20位的物理地址。

当8086CPU要读写内存时：  
（1）CPU中的相关部件提供两个16位的地址，一个称为段地址，另一个称为偏移地址；  
（2）段地址和偏移地址通过内部总线送入一个称为地址加法器的部件；  
（3）地址加法器将两个16位地址合成为一个20位的物理地址；  
（4）地址加法器通过内部总线将20位物理地址送入输入输出控制电路；  
（5）输入输出控制电路将20位物理地址送上地址总线；  
（6）20位物理地址被地址总线送到存储器。

地址加法器采用物理地址 = 段地址X16 + 偏移地址的方法用段地址和偏移地址合成物理地址。

### **2.7  “段地址X16+偏移地址=物理地址”的本质含义**
“段地址X16+偏移地址=物理地址”的本质含义是：CPU在访问内存时，用一个基础地址（段地址X16）和一个相对基础地址的偏移地址相加，给出内存单元的物理地址。

举个例子：

假如说隔壁部门的同事张三来找你询问李四的工位？你发现李四的工位不好直接描述，既不是在角落也不是在中间。这时你发现李四旁边坐着的是经理，所以你告诉张三说李四就是经理左边第二位的那个人。这就是生活中使用“基础地址+偏移地址=物理地址”的例子。  

还有比方说，大家描述学校水房的位置，一般会说在几号餐厅后面，或某宿舍楼旁边。

### **2.8 段的概念**
CPU访问内存单元时，必须向内存提供内存单元的物理地址。8086CPU在内部用段地址和偏移地址移位相加的方法形成最终的物理地址。

CPU可以用不同的段地址和偏移地址形成同一个物理地址。

可以根据需要，将地址连续、起始地址为16的倍数的一组内存单元定义为一个段。

### **2.9 段寄存器***

8086CPU内部有四个段寄存器：CS、DS、SS、ES。用于存储指定内存单元的段地址。

### **2.10 CS和IP**

8086PC机中，任意时刻，CPU将CS:IP指向的内容当作指令执行。其中CS为代码段寄存器，IP为指令指针寄存器。

8086CPU执行指令过程如下：

（1） 从CS：IP指向的内存单元读取指令，读取的指令进入指令缓冲器；  
（2） IP = IP + 所读指令的长度，从而指向下一条指令；  
（3） 执行指令。转到步骤（1），重复这个过程。  

### **2.11 修改CS、IP的指令**
能够改变CS、IP内容的指令被统称为转移指令。如jump指令。

### **2.12 代码段**
我们可以将长度为N(N <= 64KB)的一组代码，存在一组地址连续、起始地址为16的倍数的内存单元中。这段地质连续的内存空间就称之为代码段。简单来说也就是存放代码的段。

### **2.9~2.12小结**
>（1） 段地址在8086CPU的段寄存器中存放。当8086CPU要访问内存时，由段寄存器提供内存单元的段地址。8086CPU有4个段寄存器，其中CS用来存放指令的段地址。  
（2） CS存放指令的段地址，IP存放指令的偏移地址。8086机中，任意时刻，CPU将CS:IP指向的内容当做指令指向。  
（3） 8086CPU工作过程：略  
（4） 8086CPU提供转移指令修改CS、IP的内容。  

---

## **第三章 寄存器（内存访问）**

本章从内存访问的角度学习相关寄存器。

### **3.1 内存中字的存储**
字单元：存放一个字型数据（16位）的内存单元，由两个地址连续的内存单元组成。高地址内存单元存放字型数据的高位字节，低地址单元存放字型数据的低位字节。

这种存储方式也被称为小端存储，Intel系列的处理器一般都是小端存储。

### **3.2 DS和[Address]**

上一章我们学习了CS段寄存器，用于存放代码段段地址。这里我们再引入另外一个段寄存器DS，用于存放数据段段地址。

需要特别注意的是，8086CPU不支持将数据直接送入段寄存器。 包括所有的段寄存器CS、DS、SS、ES都不支持将数据从内存直接送入。内存中的数据必须先送入其他中间寄存器，然后在从中间寄存器送入段寄存器。（此处描述有误：栈操作"pop 段寄存器"实际上就是将数据从内存中直接送入段寄存器，此处应该更正为无法通过move指令将数据从内存中直接送入段寄存器）

"[address]"表示一个内存单元，中括号中的address表示内存单元的偏移地址。默认情况下，8686CPU取DS中的数据作为该内存单元的段地址。

### **3.3 字的传送**

使用move指令一次可以传送一个字。move指令可以将数据从内存送入寄存器，也可以将数据从寄存器送入内存，也可以将数据从寄存器送入寄存器。**但move指令不支持内存到内存的传送。**

### **3.4 mov、add、sub指令**

add指令和sub指令与mov指令用法类似，他们都有两个操作对象。这两个操作对象可以是如下格式：  
寄存器， 数据  
寄存器， 寄存器  
寄存器， 内存单元  
内存单元， 寄存器

有两点需要注意：  
（1） mov、add、sub指令的两个操作对象不能同时为内存单元。
（2） 段寄存器只能接收mov指令传送数据，不可以进行算术运算。如 add ds, ax指令是违法的。（此处描述不够严谨，实际上段寄存器也可以接收来自操作栈的pop指令传递的数据）

### **3.5 数据段**
数据段是一段长度为N(N <= 64KB)、地址连续、其实地址为16的倍数的内存单元。我们用段寄存器DS存放数据段的段地址。

### **3.1~3.5小结**
> （1） 字在内存中存储时，要用两个地址连续的内存单元来存放，字的低位字节存放在低地址单元中，高位字节存放在高地址单元中。  
（2） 用mov指令访问内存单元，可以在mov指令中只给出单元的偏移地址，此时，段地址默认在DS寄存器中。  
（3）[address]表示一个偏移地址为address的内存单元。  
（4） 在内存和寄存器之间传送数据时，高地址单元和高8位寄存器、低地址单元和低8位寄存器相对应。  
（5） mov、add、sub是具有两个操作对象的指令。jmp是具有一个操作对象的指令。  
（6） 可以根据自己的推测，在debug中实验指令的新格式。
>

### **3.6 栈**
栈就是一种先进后出的数据结构。LIFO(Last In First Out)。

### **3.7 CPU提供的栈机制**
8086CPU对栈提供两个基本操作指令:PUSH（入栈）和POP（出栈）。 PUSH是将数据送入栈中，POP是将数据移出栈中。

前面我们已经学习了CS和DS两个段寄存器。并且知道CS:IP指向的内存单元被当做指令，DS:[address]指向的内存单元被当做数据。这里我们引入另外一个段寄存器SS，SS中保存的是栈顶元素的段地址，此外使用SP保存栈顶元素的偏移地址。故在任意时刻SS:SP都指向栈顶元素。

PUSH AX的操作详情：  
（1）SP=SP-2，SS:SP指向当前栈顶前面的单元，以当前栈顶前面的单元为新的栈顶；  
（2）将ax中的内容送入SS:SP指向的内存单元，SS:SP此时指向新的栈顶。

POP AX的操作详情：  
（1）将SS:SP指向的内存单元处的数据送入ax中；  
（2）SP=SP+2，SS:SP指向当前栈顶下面的单元，以当前栈顶下面的单元为新的栈顶。

### **3.8 栈顶超界问题**

当栈满的时候进行PUSH操作或者栈空的时候使用POP操作，都将引发栈顶超界问题。

8086CPU并未对栈顶超界做任何处理，程序员在编程的时候应当避免使得栈顶超界的情况发生。

### **3.9 push、pop指令**
push指令和pop指令支持如下形式：  
```
push 寄存器
push 段寄存器
push 内存单元

pop 寄存器
pop 段寄存器
pop 内存单元
```
push、pop实际上就是一种内存传送指令，可以在寄存器和内存之间传送数据，与mov指令不同的是，push和pop指令访问的内存单元的地址不是在指令中给出的，而是由SS:SP指出的。同时，push和pop还要改变sp中的值。CPU执行mov指令仅需一步，CPU执行push和pop指令需要两步：传送数据和修改sp的值。

需要注意的是，push、pop等栈操作指令，修改的只是SP，也就是说，栈顶的变化范围最大为：0~FFFFH。


>**栈的综述**  
（1）8086CPU提供了栈操作机制，方案如下。  
在SS、SP中存放栈顶的段地址和偏移地址；  
提供入栈和出栈指令，它们根据SS:SP指示的地址，按照栈的方式访问内存单元。  
（2）push指令的执行步骤：1、 SP=SP-2； 2、向SS:SP指向的字单元中送入数据。  
（3）pop指令的执行步骤：1、从SS:SP指向的字单元中读取数据；2、SP=SP+2。  
（4）任意时刻，SS:SP指向栈顶元素。
（5）8086CPU只记录栈顶，栈空间的大小我们要自己管理。  
（6）用栈来暂存以后要恢复的寄存器的内容时，寄存器出栈的顺序要和入栈的顺序相反。  
（7）push、pop实际上是一种内存传送指令，注意它们的灵活应用。  
栈是一种非常重要的机制，一定要深入理解，灵活掌握。  (P67)

### **3.10 栈段**
与代码段、数据段类似，我们在编程时，可以根据需要，将一组内存单元定义为一个段。我们可以将长度为N(N<=64KB)的一组地址连续、起始地址为16的倍数的内存单元，当做栈空间来用。只需要使用SS:SP指向它们。

一个栈最大为64KB，即偏移地址所能指向的最大范围。当一个大小为64KB的栈，其SP=0时则表示该栈为空或者栈满。

> **段的综述**  
我们可以将一段内存定义为一个段，用一个段地址指示段，用偏移地址访问段内的单元（通过偏移地址的移动来访问段内的单元）。这完全是我们自己的安排。
>
>我们可以用一个段存放数据，将它定义为“数据段”；  
我们可以用一个段存放代码，将它定义为“代码段”；  
我们可以用一个段当做栈，将它定义为“栈段”；  
>
>我们可以这样安排，但若要让CPU按照我们的安排来访问这些段，就要：
>
> 对于数据段，将它的段地址放在DS中，用mov、add、sub等访问内存单元的指令时，CPU就将我们定义的数据段中的内容当做数据来访问；  
>
对于代码段，将它的段地址放在CS中，将段中第一条指令的偏移地址放在IP中，这样CPU就将执行我们定义的代码段中的指令；  对于栈段，将它的段地址放在SS中，将栈顶单元的偏移地址放在SP中，这样CPU在需要进行栈操作的时候，比如执行push、pop指令等，就将我们定义的栈段当做栈空间来用。
>
>可见，不管我们如何安排，CPU将内存中的某段内容当做代码，是因为CS:IP指向了那里；CPU将某段内存当做栈，是因为SS:SP指向了那里。我们一定要清楚，什么是我们的安排，以及如何让CPU按我们的安排行事。要非常清楚CPU的工作原理，才能在控制CPU按照我们安排运行的时候做到游刃有余。
>
>比如我们将10000H~1001FH安排为代码段，并在里面存储如下代码：
```
mov ax, 1000H   
mov ss, ax  
mov sp, 0020H         ;初始化栈顶  
mov ax, cs  
mov ds, ax            ;设置数据段段地址  
mov ax, [0]
add ax, [2]
mov bx, [4]
add bx, [6]
push ax
push bx
pop ax
pop bx
```
>设置CS=1000H，IP=0，这段代码将得到执行。可以看到，在这段代码中，我们又将10000H~1001FH安排为栈段和数据段。10000H~1001FH这段内存，即是代码段，又是栈段和数据段。
>
>一段内存，可以即是代码的存储空间，又是数据的存储空间，还可以是栈空间，也可以什么也不是。关键在于CPU中寄存器的设置，即CS、IP，SS、SP，DS的指向。
>
>（p69）

---
## **第四章 第一个程序**

### **4.1 一个源程序从写出到执行的过程**

一个汇编语言程序从写出到最终执行主要经历三步：  

第一步：编写汇编语言程序；  
第二步：对源程序进行编译连接；  
第三步：执行可执行文件中的程序。

对源程序进行编译连接生成可在操作系统中直接运行的可执行文件。可执行文件包含两部分内容。
- 程序（从源程序中的汇编指令翻译过来的机器码）和数据（源程序中定义的数据）
- 相关的描述信息（比如，程序有多大、要占用多少内存空间等）

### **4.2 源程序**

一段简单的汇编语言源程序：
```c
assume cs:codesg

codesg segment

    mov ax, 0123H
    mov bx, 0456H
    add ax, bx
    add ax, ax

    mov ax, 4c00H
    int 21H

codesg ends

end
```
#### **1. 伪指令**  
在汇编语言源程序中，包括两种指令，一种是汇编指令，一种是伪指令。汇编指令是有对应的机器码的指令，可以被编译为机器指令，最终为CPU所执行。而伪指令没有对应的机器指令，最终不被CPU所执行。伪指令是由编译器来执行的指令，编译器根据伪指令来进行相应的编译工作。   

下面介绍上面程序中所出现的几个伪指令：

**（1） segment和ends **

segment和ends是一对成对使用的伪指令。功能是定义一个段。使用格式为：  
```
段名 segment
.
.
段名 ends
```
一个汇编语言程序是由多个段组成的，这些段被用来存放代码（代码段）、数据（数据段）或当做栈空间（栈段）来使用。

**（2）end  **

end是一个汇编语言的结束标记，编译器在编译汇编程序的过程中，如果碰到了伪指令end，就结束对源程序的编译。

**（3）assume **  

这条伪指令的含义为“假设”。它假设某一段寄存器和程序中的某一个用segment...ends定义的段相关联。即将定义的段的段地址存放在段寄存器中。

#### **2. 源程序中的“程序”**  

我们将源程序文件中的所有的内容称之为源程序，将源程序中最终由计算机执行、处理的指令或数据，称为程序。  

#### **3. 标号**

汇编源程序中，除了汇编指令和伪指令外，还有一些标号，比如“codesg”。**一个标号指代了一个地址**。比如codesg在segment的前面，作为一个段的名称，这个段的名称最终将被编译、连接程序处理为一个段的段地址。

#### **4. 程序的结构**

源程序就是由一些段组成的。我们可以在这些段中存放代码、数据、或将某个段当做栈空间。

#### **5. 程序返回**
一个程序结束后，将CPU控制权交还给使它得以运行的程序，我们称这个过程为**程序返回**。  程序返回指令：
```c
mov ax, 4c00H
int 21H
```
当前我们不必理解这两天语句的含义。只要记住使用这两条指令可以实现程序返回。

**段结束、程序结束、程序返回的区别**  

|目的               |相关指令    |指令性质    |指令执行者         |
|-----------       |------------|-----------|------------       |
|通知编译器一个段结束|段名 ends   |伪指令      |编译时，由编译器执行|
|通知编译器程序结束  | end        |伪指令     |编译时，由编译器执行 |
|程序返回           |mov ax,4c00H int 21H| 汇编指令| 执行时，由于CPU执行|

#### **6. 语法错误和逻辑错误**

一般来说，程序在编译时被编译器发现的错误是语法错误，如 mov ss, 1234 。

在程序编译后，在运行时发生的错误是逻辑错误，如除零操作。

### **4.3 编辑源程序**

源程序文件以.asm作为后缀。

### **4.4 编译**

我们使用微软的masm5.0汇编编译器进行编译，文件名为masm.exe。我们以c:\1.asm为例说明编译源程序的方法步骤。  

（1）进入DOS方式，运行masm.exe。
```
...
...
Source filename [.ASM]: _
```

（2）输入要编译的源程序文件名，按enter键。
```
Source filename [.ASM]: c:\1.asm
Object filename [1.obj]:
```
输入要编译出的目标文件名，如果不输入则默认使用源程序名。

（3）确定了目标文件名称后，继续按Enter键（两次），忽略中间文件的生成。  

最终完成对源程序的编译，生成目标文件。目标文件以.obj作为后缀。

### **4.5 连接**

在对源程序进行编译生成目标文件后，我们需要对目标文件进行连接，从而得到可执行文件。  

这里我们使用微软的Overlay Linker3.60连接器，文件名为link.exe。我们以4.4中生成的目标文件c:\masm\1.obj为例说明连接操作步骤。

（1）进入DOS方式，运行link.exe。
```
...
...
Object Modules [.OBJ]: _
```

（2）输入目标文件名，按enter键。  
```
...
...
Object Modules [.OBJ]: 1.obj
Run File [1.EXE]: _
```
键入生成可执行文件的名称。如果不输入则默认使用源程序名。

（3）确定了可执行文件名后，按Enter键（两次），忽略镜像文件的生成，忽略库文件的连接。

经过以上三步后，最终会生成以.exe结尾的可执行文件。

**连接的作用**  

（1）当源程序很大时，可以将它分为多个源程序文件来单独编译，然后将生成的目标文件连接在一起，节约程序编译时间。

（2）程序中调用了某个库文件的子程序，需要将这个库文件和该程序生成的目标文件连接在一起，生成一个可执行文件。

（3）一个源程序编译后，达到了存有机器码的目标文件，目标文件中的有些内容还不能直接用来生成可执行文件，连接程序将这些内容处理为最终的可执行信息。所以，在只有一个源程序文件，而又不需要调用某个库中的子程序的情况下，也必须用连接程序对目标文件进行处理，生成可执行
文件。

### **4.6 以简化的方式进行编译和连接**
一键编译：
```
masm c:\1.asm;
```

一键连接：
```
link c:\1.obj;
```

### **4.7 1.exe的执行**

进入dos环境，直接键入.exe可执行文件的文件名即可执行。

### **4.8 谁将可执行文件中的程序装载进入内存并使它运行**

（1）在DOS中直接执行1.exe时，是正在运行的command，将1.exe中的程序加载入内存；  

（2）command设置CPU的CS:IP指向程序的第一条指令（即程序入口），从而使程序得以运行。

（3）程序运行结束后，返回到command中，CPU继续运行command。

> **操作系统的外壳**
>
>操作系统是由多个功能模块组成的庞大、复杂的软件系统。任何通用的操作系统，都要提供一个称为shell(外壳)的程序，用户（操作人员）使用这个程序来操作计算机系统进行工作。
>
>DOS中有一个程序command.exe，这个程序在DOS中称为命令解释器，也就是DOS系统的shell。
>
>DOS启动时，先完成其他重要的初始化工作，然后运行command.exe，command.exe运行后，执行完其他的相关任务后，在屏幕上显示出由当前盘符和当前路径组成的提示符，比如：“c:\”或“c:\windows”等，然后等待用户的输入。
>
>用户可以输入要执行的命令，比如，cd、dir、type等，这些命令由于command执行，command执行完这些命令后，再次显示当前盘符和当前路径组成的提示符，等待用户输入。
>
>如果用户要执行一个程序，则输入该程序可执行文件的名称，command首先根据文件名找到可执行文件，然后将这个可执行文件中的程序加载入内存，设置CS:IP指向程序的入口。此后，command暂停运行，CPU运行程序。程序运行结束后，返回到command中，command再次显示由当前盘符和当前路径组成的提示符，等待用户输入。
>
>在DOS中，command处理各种输入：命令或要执行的程序的文件名。我们就是通过command来进行工作的。

**shell : 操作人员和OS之间的API。**

>**汇编程序从写出到执行的过程**
>
>到此，完成了一个汇编程序从写出到执行的全部过程。我们经历了这样一个历程：  编程（Edit） →  1.asm → 编译（masm） → 1.obj → 连接（link） → 1.exe → 加载（command） → 内存中的程序 → 运行（CPU）

### **4.9 程序执行过程的跟踪**

**DOS系统中.exe文件中程序的加载过程**  

（1）找到一段起始地址为SA:0000（即起始地址的偏移地址为0）的容量足够的空闲内存区；

（2）在这段内存区的前256个字节中，创建一个称为程序段前缀（PSP）的数据区，DOS要利用PSP来和被加载程序进行通信；

（3）从这段内存区的256字节处开始（在PSP后面），将程序装入，程序的地址被设为SA+10H:0；（空闲内存区从SA:0开始，0~255字节为PSP，从256字节处开始存放程序，为了更好地区分PSP和程序，DOS一般将它们划分到不同的段中，所以有了这样的地址安排：  
空闲内存区：SA:00  
PSP区：SA:0  
程序区：SA+10:0   
注意：PSP和程序区虽然物理地址连续，却有着不同的段地址
）  

（4）将该内存区的段地址存取ds中，初始化其它相关寄存器后，设置CS:IP指向程序入口。

程序加载进内存后，cx中存放的是程序的长度，ds存放着程序所在内存区的段地址，cs存放可执行程序的段地址，ip存放着可执行程序的偏移地址。

**Debug常用命令**  

我们使用Debug对程序的执行过程进行跟踪。  
用T命令单步执行程序的每一条执行。  
用P命令执行程序结束语句int 21。  
用Q命令退出debug。

---

## **第五章  [BX]和loop指令**

**1. [bx]和内存单元的描述**  


[bx]表示一个**内存单元**，该内存单元的段地址位于ds中，偏移地址位于bx中。
该内存单元的完整地址为： ds*16 + bx。

**2. loop**

循环指令。指令格式为：loop 标号  
该指令分两步执行。  

第一步，计算 cx = cx -1  
第二步，判断cx中的值，不为零则跳转至标号出执行程序，如果为零则向下执行。

**3. 我们定义的描述性的符号：“（）”**

“（）”表示一个内存单元或寄存器的内容。也即是存储器中存储的值。

“（）”中的元素可以有3中类型：寄存器名、段寄存器名、内存单元的物理地址（一个20位数据）。

**4. 约定符号idata表示常量**

在以后的学习中我们约定idata表示一个常量。

### **5.1 [BX]**

[bx]表示一个**内存单元。**

mov ax, [bx] 代码的含义：将ds:bx所指向内存单元的内容放入ax寄存器中。即：(ax)=((ds*16)+(bx))

mov [bx], ax 代码的含义：将ax中的内容放入ds:bx所指向的内存单元中。即：((ds*16)+(bx))=(ax)

### **5.2 Loop指令**
首先loop指令的格式是：loop 标号。该指令分两步执行：

第一步， 计算cx = cx - 1；  
第二步，判断cx中的值，不为零则跳转至标号处执行程序，如果为零则向下执行。

一般使用loop指令实现循环功能。格式如下：
```c
    mov cx, n
s:  add ax, ax
    loop s
```
以上代码会循环执行n次。（n >= 0）

### **5.3 在Debug中跟踪用loop指令实现的循环程序**
使用Debug调试程序时，有几条经常用到的指令。

**T指令**，单步执行指令。

**g指令**, 跳至断点，从当前IP执行至指定IP处。"g 0012"表示程序由当前位置执行至DS:0012处。

**p指令**，循环执行指令，p指令用于执行完当前次数。

### **5.4 Debug和汇编编译器masm对指令的不同处理**
Debug和汇编编译器masm对形如“mov ax, [0]”这类指令的处理是不同的。debug将"[0]"看做是一个内存单元，该内存单元的地址是 ds*6 + 0。而编译器直接将“[0]”看做立即数0。因此有如下约定。

（1）在汇编源程序中，如果指令访问一个内存单元，则在指令中必须用"[...]"来表示内存单元，如果在“[...]”里用一个常量idata直接给出内存单元的偏移地址，就要在“[]”的前面显式的给出段地址所在的段寄存器。

（2）如果在“[]”里面用寄存器，比如bx,间接给出内存单元的偏移地址，则段地址默认在ds中。当然，也可以显式的给出段地址所在的段寄存器。

以上两点概括来说就是，如果内存单元的偏移地址使用立即数给出，则必须显式指明其段地址所在的段寄存器。

### **5.5 loop和[bx]的联合应用**
通过loop和[bx]联合应用实现对连续内存单元的操作实例：
```c
    ...
    mov bx, 0
    mov cx, 50

s:  mov ax, [bx]
    inc bx
    loop s
    ...
```
以上代码通过循环实现了对内存单元DS:0000H~DS:0032H内容的操作。

### **5.6段前缀**
如果内存单元的偏移地址由bx给出，如“mov ax, [bx]”，则段地址默认位于ds中。我们也可以在访问内存单元的指令中显式的给出内存单元段地址所在的段寄存器。比如：

（1）mov ax, ds:[bx]  
（2）mov ax, cs:[bx]  
（3）mov ax, ss:[bx]  
（4）mov ax, cs:[bx]  
（5）mov ax, ss:[0]  
（6）mov ax, cs:[0]

这些出现在访问内存单元的指令中，用于显式的指明内存单元的段地址的“ds:”、“cs:”、“ss:”、“es:”，在汇编语言中称为**段前缀**。

### **5.7 一段安全的空间**
（1）我们需要直接向一段内存汇总写入内容；

（2）这段内存空间不应当存放系统或其他程序的数据或代码，否则写入操作很可能引发错误；

（3）DOS方式下，一般情况，0:200~0:2ff空间中没有系统或其他程序的数据或代码；

（4）以后，我们需要直接向一段内存中写入内容时，就使用0:200~0:2ff这段空间。

### **5.8 段前缀的使用**
当需要操作的内存空间跨段时，显式的使用段前缀给出内存单元的段地址，可以避免在循环中对ds的重复设置。

也即是说一个内存单元的段地址不仅仅可以由ds给出，也可以通过cs、ss、es给出。

---

## **第六章 包含多个段的程序**
程序取得所需空间的方法有两种，一是在加载程序的时候为程序分配，再就是程序在执行的过程向系统分配。在本课程中，我们只讨论第一种方法。

### **6.1 在代码段中使用数据**
下面一段代码用于计算8个数据的累加和，结果放在ax寄存器中：

```c
assume cs:code

code segment

  dw 0123h, 0456h, 0789h, 0abch, 0defh, 0fedh, 0cbah, 0987h

start mov bx, 0
      mov ax, 0

      mov cx, 8
  s:  add ax, cs:[bx]
      add bx, 2
      loop s

  mov ax, 4c00H
  int 21H
code ends
end start
```
分析这段代码，我们使用dw定义了8个字型数据，并且使用 “end 标号”的形式指明了程序的入口。

### **6.2 在代码段中使用栈**
```c
assume cs:codesg

codesg segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

start:  mov ax, cs
        mov ss, ax
        mov sp, 30h

        mov bx, 0
        mov cx, 8
    s:  push cs:[bx]
        add bx, 2
        loop s

        mov bx, 0
        mov cx, 8
    s0: pop cs:[bx]
        add bx, 2
        loop s0

    mov ax, 4c00h
    int 21h

codesg ends
end start
```

### **6.3 将数据、代码、栈放入不同的段**

```c
assume cs:code, ds:data, ss:stack

data segment
  dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends

stack segment
  dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
stack ends

code segment

start:  mov ax, stack
        mov ss, ax
        mov sp, 20h

        mov ax, data
        mov ds, ax

        mov bx, 0

        mov cx, 8
    s:  push [bx]
        add bx, 2
        loop s

        mov bx, 0

        mov cx, 8
   s0:  pop [bx]
        add bx, 2
        loop s0

      mov ax, 4c00h
      int 21h

code ends

end start

```
下面对以上代码进行说明。

（1）定义多个段的方法  

定义数据段、栈段与定义代码段的方法没有区别，只是对于不同的段，要有不同的段名。

（2）对段地址的引用

在程序中，段名就相当于一个标号，它代表了段地址。例如程序中“data”段中的数据“0abch”的地址就是：data:6。要将它送入bx中，代码如下：
```c
mov ax, data
mov ds, ax
mov bx, ds:[6]
```
（3）“代码段”、“数据段”、“栈段”完全是我们的安排

我们通过“end 标号”的形式来声明程序的入口地址，这个入口信息被写入可执行文件中的描述信息中。可执行文件中的程序被加载入内存后，CPU的CS:IP就会被设置指向这个入口。

我们通过如下代码来指定程序的栈段：
```c
mov ax, stack
mov ss, ax
mov sp, 20h
```
通过如下代码来指定数据段：
```c
mov ax, data
mov ds, ax
```
总而言之，CPU到底如何处理我们定义的段中的内容，是当作指令执行，当作数据访问，还是当作栈空间，完全靠程序中具体的汇编指令，和汇编指令对CS:IP、SS:SP、DS等寄存器的设置来决定的。

---
## **第七章 更灵活的定位内存地址的方法**
本章主要讲不同的寻址方式。

### **7.1 and和or指令**
and表示逻辑与。or表示逻辑或。

### **7.2 关于ASCII码**
ASCII码：American Standard Code for Information Interchange，美国信息交换标准代码。用8位（一个字节）二进制数表示一个字符。起初定义了128个字符，后来扩展至256个。

当我们再键盘上按下字母a键，屏幕上显示a字母，这其中经历了哪些过程？
1. a被ASCII编码为数字61H存储在指定内存空间内。
2. 文本编辑器软件从内存中取出61H,将其送入显卡显存中。
3. 显卡根据ASCII编码将61H反译为字母a，同时显卡驱动显示器，将字母a的图像画在屏幕上。

通过以上3步，我们就看到了字母a被显示在屏幕上。

### **7.3 以字符形式给出的数据**
在汇编程序中，使用引号‘’括起来的内容被识别为字符，编译器将把它转换为对应的ASCII码。

### **7.4 大小写转换的问题**
在ASCII码中，小写字母的对应范围为：61H - 7AH。大写字母的对应范围为：41H - 5AH。可见同一个字母的大写形式的ASCII码比小写形式的ASCII码小20H。

仔细观察大小写字母所对应的ASCII吗二进制形式，可以发现如下规律：大写字母从右数第6位（从1开始计算）全为0，小写字母从右数第6位全为1。

综上我们可以总结出大小写转换的两种方式：

**字母大小写转换方式1：**
1. 大写字母加上20h可转换为小写字母。
2. 小写字母减去20h可转换为大写字母。

**字母大小写转换方式2：**
1. 字母转大写：逻辑与11011111B。
2. 字母转小写：逻辑或00100000B。

### **7.5 [bx+idata]**
这是一种"变量+常量"的寻址方式。

[bx+idata]表示一个内存单元，它的偏移地址为(bx)+idata。  
指令:
```c
mov ax, [bx+200]
```
表示将一个内存单元的内容送入ax，这个内存单元的长度为2个字节（字单元），存放一个字，偏移地址为bx中的数值加上200，段地址在ds中。

该指令的常用格式有：
```c
mov ax, [bx+200]
mov ax, 200[bx]
mov ax, [bx].200
```

### **7.6 用[bx+idata]的方式进行数组的处理**
我们可以将地址连续的多个数据当做数组处理。例如定义如下数据：
```c
datasg segment
  db 'BaSiC'
  db 'MinIX'
datasg ends
```
我们可以把如上两个字符串当做两个数组，一个数组下标从0开始，一个数组下标从5开始。在程序中使用[bx+0]和[bx+5]的方式定位两个字符串的首地址。从而可以在一个循环当中同时处理两组数据。

回忆我们在高级语言中用到的数组取值方式(如c或java)：a[index]。可以看出这就是汇编语言中[bx+idata]形式的变种。a与idata相对应，是一常量，表示了数组的首地址。而下标index与bx对应，是一变量，表示数组下标。

### **7.7 SI和DI**
SI是Source Index的缩写。DI是Destination Index的缩写。它俩的功能与bx相近，但SI和DI不能够分成两个8位寄存器来使用。下面三组指令实现了相同的功能：

```c
;(1)
mov bx, 0
mov ax, [bx]

;(2)
mov si, 0
mov ax, [si]

;(3)
mov di, 0
mov ax, [di]
```
下面的三组指令也实现了相同的功能：

```c
;(1)
mov bx, 0
mov ax, [bx+123]

;(2)
mov si, 0
mov ax, [si+123]

;(3)
mov di, 0
mov ax, [di+123]
```

### **7.8 [bx+si]和[bx+di]**
这是一种“变量+变量”的寻址方式。

[bx+si]和[bx+di]含义相似，都是表示一个内存单元。该内存单元的段地址位于ds中，偏移地址为bx的值加上si的值（或bx的值加上di的值）。

该指令的常用格式有：

```c
mov ax, [bx+si]
mov ax, [bx][si]
```

### **7.9 [bx+si+idata]和[bx+di+idata]**
这是一种"变量+变量+常量"的寻址方式。

常用指令格式：

```c
mov ax, [bx+200+si]
mov ax, [200+bx+si]
mov ax, 200[bx][si]
mov ax, [bx].200[si]
mov ax, [bx][si].200
```
### **7.10 不同的寻址方式的灵活应用**
总结一下前面讲到的几种定位内存地址的方法（寻址方式）：

（1）[idata]用一个常量来表示地址，可用于直接定位一个内存单元；  
（2）[bx]用一个变量来表示内存地址，可用于间接定位一个内存单元；  
（3）[bx+idata]用一个变量和常量表示地址，可在一个起始地址的基础上用变量间接定位一个内存单元；  
（4）[bx+si]用两个变量表示地址；  
（5）[bx+si+idata]用两个变量和一个常量表示地址。


下一章中，我们将对寻址方式的问题进行更深入的探讨，之所以如此重视这个问题，是因为寻址方式的适当应用，使我们可以以更合理的结构来看待所要处理的数据。而**为所要处理的看似杂乱的数据设计一种清晰的数据结构是程序设计的一个关键问题**（个人认为这段话说的很有道理，特记录于此）

---

## **第八章 数据处理的两个基本问题**
本章旨在进一步加强对不同寻址方式的理解及运用。

计算机是进行数据处理、运算的机器，这其中包含两个基本的问题：  

（1）处理的数据在什么地方？  
（2）要处理的数据有多长？

携带着这两个问题，我们开启第八章的学习之路。

### **8.1 bx、si、di和bp**

首先看下这四个寄存器的含义：

bx, Base,Pointer to base addresss (data)。一般用于存储数据段的基址（首地址）。  
si，Source Index，Source string/index pointer。一般用于存储源数组数据索引（下标）。  
di，Destination Index，estination string/index pointer。一般用于存储目标数组数据索引（下标）。  
bp，Base Pointer，Pointer to base address (stack)。一般用于存储栈的基址。

然后在使用过程中有几处需要注意的地方：

（1）在8086CPU中，只有这4个寄存器可以用在“[....]”中来进行内存单元的寻址。其他寄存器是不可以的，例如“mov bx, [ax]”就是错误的用法。

（2）在[...]中，这四个寄存器可以单个出现，或只能以4种组合出现：bx和si、bx和di、bp和si、bp和di。为了方便记忆可以将si和di看做一组，将bx和bp看做一组。组间可以自由组合，组内不能组合。（脑补为人类不可以近亲繁殖。）

（3）只要在[...]中使用寄存器bp，而指令中没有显性的给出段地址，则段地址就默认在ss中。

### **8.2 机器指令处理的数据在什么地方**

这是我们在开头抛出的两个问题中的第一个。

在指令执行前，所要处理的数据可以在3个地方：CPU内部、内存、端口（端口暂时不用知道是什么东西）。

我们知道了存储数据的部件，但如果具体找到这些部件存储的数据位置？下一节将解答我们的疑问。

### **8.3 汇编语言中数据位置的表达**

在汇编语言中如何表达数据的位置？  

汇编语言中用3个概念来表达数据的位置。

（1）立即数

对于直接包含在机器指令中的数据（执行前在CPU的指令缓冲器中），汇编语言中称为立即数（idata）,在汇编指令中直接给出。
例如：  
```c
mov ax, 1
or bx, 00100000B
```

（2）寄存器

指令要处理的数据在寄存器中，在汇编指令中给出相应的寄存器名。例如：
```c
mov ax, bx
push bx
```

（3）段地址（SA）和偏移地址（EA）

指令要处理的数据在内存中，在汇编语言中可以用[X]的格式给出EA，SA在某个段寄存器中。

存放段地址的寄存器可以是默认的，也可以显性给出。例如：
```c
;段寄存器默认存储在DS中
 mov  ax, [0]
 mov ax, [bx]
 mov ax, [di]
 ...

 ;段寄存器默认存储在SS中
 mov ax, [bp]
 mov ax, [bp+si]
 mov ax, [bp+di]
 ...

 ;段寄存器显性给出
 mov ax, ds:[bp]
 mov ax, es:[3]
 mov ax, ss:[bx+si]
 mov ax, cs:[bx+si+8]
```

### **8.4 寻址方式**

这一节我们总结一下所学到过的寻址方式。列表如下：

|寻址方式| 含义 | 名称 | 常用格式举例|备注|
|--------|----|---|---|
|[idata]|EA=idata; SA=(ds)| 直接寻址|[idata]|偏移地址=立即数|
|[bx]  |EA=(bx); SA=(ds)| 寄存器间接寻址|[bx]|偏移地址=变量|
|[bx+idata]| EA=(bx)+idata; SA=(ds)|寄存器相对寻址| 用于结构体：[bx].idata; 用于数组：idata[si],idata[di]; 用于二维数组：[bx][idata]   |偏移地址=变量+立即数|
|[bx+si]|EA=(bx)+(si); SA=(ds)|基址变址寻址|用于二维数组：[bx][si]|偏移地址=变量+变量|
|[bx+si+idata]|EA=(bx)+(si)+idata; SA=(ds)|相对基址变址寻址| 用于表格（结构）中的数组项：[bx].idata[si]; 用于二维数组idata[bx][si]|偏移地址=变量+变量+立即数|

注意在8.1节指出的特殊情况，只要在[...]中使用寄存器bp，而指令中没有显性的给出段地址，段地址就默认在ss中。

### **8.5 指令要处理的数据有多长**

这是我们在开头抛出的两个问题中的第二个。

8086CPU的指令，可以处理两种尺寸的数据，byte和word。所以在机器指令中要指明，指令进行的是字操作还是字节操作。对于这个问题，汇编语言中用以下方法处理。

（1）通过寄存器名指定要处理的数据的尺寸。如果寄存器名是字型寄存器（如ax、bx等），则说明指令进行的是字操作。如果寄存器名是字节型寄存器（如al、ah、bl等），则说明指令进行的是字节操作。

（2）在没有寄存器名存在的情况下，用操作符X ptr指明内存单元的长度，X在汇编指令中可以为word或byte。这种情形适用于没有寄存器参与的内存单元访问指令中。例如：
```c
;下面的指令，用word ptr指明了指令中访问的内存单元是一个字单元
mov word ptr ds:[0], 1
inc word ptr [bx]

;下面的指令，用byte ptr指明了指令访问的内存单元是一个字节单元
mov byte ptr ds:[0], 1
inc byte ptr [bx]
```

（3）其他方法。有些指令默认了访问的是字单元还是字节单元，比如，push[1000H]就不用指明访问的是字单元还是字节单元，因为push指令只会进行字操作。

### **8.6 寻址方式的综合应用**

8086CPU提供的如[bx+si+idata]的寻址方式为结构化数据的处理提供了方便。使得我们可以在编程的时候，从结构化的角度去看待所要处理的数据。正常情况下，一个结构化的数据包含了多个数据项，而数据项的类型又不相同，有的是字型数据，有的是字节型数据，有的是数组（字符串）。一般来说，我们可以用[bx+idata+si]的方式来访问结构体中的数据。用bx定位整个结构体，用idata定位结构体中的某一个数据项，用si定位数组项中的元素。为此，汇编语言提供了更为贴切的书写方式，如[bx].idata、[bx].idata[si]。

### **8.7 div指令**

div是除法指令。在使用的过程中应注意以下问题：

（1）除数：有8位和16位两种，在一个reg(寄存器)或内存单元中。

（2）被除数：默认放在AX或DX和AX中，如果除数为8位，被除数则为16位，默认在AX中存放；如果除数为16位，被除数则为32位，在DX和AX中存放，DX存放高16位，AX存放低16位。

（3）结果：如果除数为8位，则AL存储除法操作的商，AH存储除法操作的余数；如果除数为16位，则AX存储除法操作的商，DX存储除法操作的余数。

div使用格式如下：  
>div reg  
>div 内存单元
### **8.8 伪指令dd**

dd用来定义dword（双字）型数据。

### **8.9 dup**

dup(duplication的缩写)用来重复开辟内存空间。

dup指令要和db、dw、dd等数据定义伪指令配合使用，使用格式如下：

> db 重复次数 dup (重复的字节型数据)
> dw 重复次数 dup (重复的字型数据)
> dd 重复次数 dup (重复的双字型数据)

例如，如下代码表示定义了9个字节：
```c
db 3 dup (0,1,2)
```
